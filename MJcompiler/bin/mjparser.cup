package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code{:
	
	boolean errorDetected;
	
	Logger log =Logger.getLogger(getClass());
	
	public void report_fatal_error(String message, Object info) throws java.lang.Exception{
		done_parsing();
		report_error(message,info);
	}
	
	public void syntax_error(Symbol cur_token){
		report_error("\nSintaksna greska",cur_token);
	}
	
	public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception{
		report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti",cur_token);
	}
	
	public void report_error(String message, Object info){
		errorDetected=true;
		StringBuilder msg=new StringBuilder(message);
		if(info instanceof Symbol)
			msg.append(" na liniji ").append(((Symbol)info).left);
		log.error(msg.toString());
	}
	
:}

init with{:
	errorDetected=false;
:}

scan with{:
	Symbol s=this.getScanner().next_token();
	if(s!=null && s.value!=null)
		log.info(s.toString()+" "+s.value.toString());
	return s;
:}

terminal PROG, CURLYOPENBRACKET, CURLYCLBRACKET, CONST, ASSIGN, SEMICOLON, COMMA,
SQUAREOPENBRACKET, SQUARECLBRACKET, ENUM, VOID, OPENBRACKET, CLBRACKET, DOT, LENGTH, PLUS, MINUS,
PERCENT, MUL, DIV, NEW, QUESTIONMARK, COLON, EQUAL, DIFFERENT, GREATERTHEN, GREATERTHENOREQUAL, LESSTHEN, LESSTHENOREQUAL,
AND, OR, INC, DEC, IF, ELSE, BREAK, CONTINUE, RETURN, READ, PRINT, SWITCH, CASE, FOR;
terminal String IDENT;
terminal Integer NUMBER, BOOL;
terminal Character CHARACTER;


nonterminal Program, ConstVarEnumDeclList, MethodDeclList, 
ConstDeclList, VarDeclList, EnumDeclList, EnumDeclName, MethodDecl, 
ConstDecl, VarDeclaration, VarDeclarationMore, 
ConstDeclMore, EnumMemberList, EnumMember, FormParList, Constant,
FormPars, VarDeclMore, FormParMore, StatementList, Statement, SingleStatement,
DesignatorStatement, Addop, 
Mulop, ActParsZeroOne, ActParsBracketsZeroOne, ActPars, CommaExprList, 
RelopExprZeroOne, Relop, OrCondTermList, AndCondFactList, Assignop, DesignatorRest, ZeroOneExpr,
CommaNumConst, CaseList, DesignatorStatementZeroOne, ConditionZeroOne, ElseBlock, Statements, Unary, ProgramName;

nonterminal rs.etf.pp1.symboltable.concepts.Struct Factor, FactorSub, MulopFactorList, Term, Expr, AddopTermList, AddExpr;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Condition, CondFact, CondTerm, Type;

nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator, DesignatorMore, DesignatorArrName, MethodNameType;

precedence left ELSE;


// ============================================================
// PROGRAM 
// ============================================================

Program ::= (Program) PROG ProgramName ConstVarEnumDeclList CURLYOPENBRACKET MethodDeclList CURLYCLBRACKET;

ProgramName ::= (ProgramName) IDENT;

ConstVarEnumDeclList ::= 	(ConstVarEnumDeclList_const) ConstVarEnumDeclList ConstDeclList
							| 
							(ConstVarEnumDeclList_var)	ConstVarEnumDeclList VarDeclList
							|
							(ConstVarEnumDeclList_enum) ConstVarEnumDeclList EnumDeclList
							|
							(ConstVarEnumDeclList_epsilon) /* epsilon */
							;

// ============================================================
// CONST DECLARATIONS 
// ============================================================

ConstDeclList ::=  (ConstDeclList) CONST Type ConstDecl ConstDeclMore SEMICOLON;

ConstDecl ::= (ConstDecl) IDENT ASSIGN Constant;

ConstDeclMore ::= (ConstDeclMore_rek)	COMMA ConstDecl ConstDeclMore
				  |
				  (ConstDeclMore_epsilon) /* epsilon */
				  ;

Constant ::= 	(Constant_number) NUMBER
				|
				(Constant_character) CHARACTER
				|
				(Constant_bool) BOOL
				;

// ============================================================
// VAR DECLARATIONS 
// ============================================================

			  
VarDeclList ::= (VarDeclList) Type VarDeclaration VarDeclarationMore SEMICOLON;
 
VarDeclaration ::= (VarDeclaration_elem) IDENT
 					|
 					(VarDeclaration_arr) IDENT SQUAREOPENBRACKET SQUARECLBRACKET
 					;
 
VarDeclarationMore ::= (VarDeclarationMore_rek) COMMA VarDeclaration VarDeclarationMore
 					   |
 					   (VarDeclarationMore_epsilon)  /* epsilon */
 					   ; 				
 			  
// ============================================================
// ENUM DECLARATIONS 
// ============================================================

EnumDeclList ::= (EnumDeclList) ENUM EnumDeclName CURLYOPENBRACKET EnumMemberList CURLYCLBRACKET;

EnumDeclName ::=(EnumDeclName) IDENT;

EnumMemberList ::= (EnumMemberList_first) EnumMember
                   | 
                   (EnumMemberList_rek) EnumMember COMMA EnumMemberList
                   ;

EnumMember ::=  (EnumMember_num) IDENT
                |
                (EnumMember_assign) IDENT ASSIGN NUMBER
                ;
             
             
// ============================================================
// METHOD DECLARATIONS 
// ============================================================

MethodDeclList ::=  (MethodDeclList_rek) MethodDeclList MethodDecl
					|
					(MethodDeclList_endRek) MethodDecl
					;

MethodDecl ::= (MethodDecl) MethodNameType OPENBRACKET FormParList CLBRACKET VarDeclMore CURLYOPENBRACKET StatementList CURLYCLBRACKET;


MethodNameType ::=      (MethodNameType_type) Type IDENT  
						|
						(MethodNameType_void) VOID IDENT
						;

FormParList ::=  	   (FormParList_form) FormPars FormParMore
					   |
					   (FormParList_epsilon) /* epsilon */
					   ;

FormPars ::=    (FormPars_var) Type IDENT 
				|
				(FormPars_arr) Type IDENT SQUAREOPENBRACKET SQUARECLBRACKET
				;


FormParMore ::=   (FormParMore_rek) COMMA FormPars FormParMore
				  |
				  (FormParMore_epsilon) /* epsilon */
				  ;

VarDeclMore ::= (VarDeclMore_list) VarDeclList VarDeclMore
				|
				(VarDeclMore_epsilon)
				;

// ============================================================
// TYPE 
// ============================================================

Type ::= (Type) IDENT;


// ============================================================
// STATEMENTS
// ============================================================

StatementList ::=   (StatementList_rek) StatementList Statement 		
  					|
				    (StatementList_epsilon) /*epsilon*/
					;	
				  
Statement ::= 	(Statement_ss) SingleStatement
				|
				(Statement_block) Statements
				;
				
Statements ::=  (Statements) CURLYOPENBRACKET StatementList CURLYCLBRACKET;


SingleStatement ::=     (SingleStatement_first) DesignatorStatement SEMICOLON
						|
						(SingleStatement_second) IF OPENBRACKET Condition CLBRACKET Statement ElseBlock
						|
						(SingleStatement_third) BREAK SEMICOLON
						|
						(SingleStatement_fourth) CONTINUE SEMICOLON
						|
						(SingleStatement_fifth) RETURN ZeroOneExpr SEMICOLON
						|
						(SingleStatement_sixth) READ OPENBRACKET Designator CLBRACKET SEMICOLON
						|
						(SingleStatement_seventh) PRINT OPENBRACKET Expr CommaNumConst CLBRACKET SEMICOLON
						|
						(SingleStatement_eighth) SWITCH OPENBRACKET Expr CLBRACKET CURLYOPENBRACKET CaseList CURLYCLBRACKET
						|
						(SingleStatement_ninth) FOR OPENBRACKET DesignatorStatementZeroOne SEMICOLON ConditionZeroOne SEMICOLON DesignatorStatementZeroOne CLBRACKET Statement
						;
						
ElseBlock ::= 	(ElseBlock_first) ELSE Statement
				|
				(ElseBlock_epsilon)
				;

ZeroOneExpr ::= (ZeroOneExpr_first) Expr
				|
				(ZeroOneExpr_epsilon)
				;

CommaNumConst ::= (CommaNumConst_first) COMMA NUMBER
				  |
				  (CommaNumConst_epsilon)
				  ;

CaseList ::=    (CaseList_first) CASE NUMBER COLON StatementList CaseList 
				|
				(CaseList_epsilon)
				;
			
DesignatorStatementZeroOne ::=  (DesignatorStatementZeroOne_first) DesignatorStatement	
								|
								(DesignatorStatementZeroOne_epsilon)
								;  

ConditionZeroOne ::=    (ConditionZeroOne_first) Condition
						|
						(ConditionZeroOne_epsilon)
						;	
	
						
// ============================================================
// DESIGNATOR STATEMENT
// ============================================================

DesignatorStatement ::= (DesignatorStatement) Designator DesignatorRest; 

DesignatorRest ::=  (DesignatorRest_first) Assignop Expr
					|
					(DesignatorRest_second) OPENBRACKET ActParsZeroOne CLBRACKET
					|
					(DesignatorRest_third) INC
					|	 			
					(DesignatorRest_fourth) DEC
					;

Assignop ::= (Assignop) ASSIGN;	


// ============================================================
// ACTUAL PARAMETERS 
// ============================================================

ActPars ::= (ActPars) Expr CommaExprList;

CommaExprList ::=   (CommaExprList_rek) COMMA Expr CommaExprList
					|
					(CommaExprList_epsilon)
					;

ActParsZeroOne ::=  (ActParsZeroOne_ActPars) ActPars
					|
					(ActParsZeroOne_epsilon)
					; 

ActParsBracketsZeroOne ::=  (ActParsBracketsZeroOne_brackets) OPENBRACKET ActParsZeroOne CLBRACKET
							|
							(ActParsBracketsZeroOne_epsilon)
							;
							
							
							
// ============================================================
// CONDITION 
// ============================================================

Condition ::= (Condition) CondTerm OrCondTermList;

OrCondTermList ::=  (OrCondTermList_rek) OrCondTermList OR CondTerm 
					|
					(OrCondTermList_epsilon) /* epsilon */ 
					; 

CondTerm ::= (CondTerm) CondFact AndCondFactList;

AndCondFactList ::= (AndCondFactList_rek) AndCondFactList AND CondFact 
					|
					(AndCondFactList_epsilon) /* epsilon */
					;

CondFact ::= (CondFact) AddExpr RelopExprZeroOne;

RelopExprZeroOne ::=    (RelopExprZeroOne_relopExpr) Relop AddExpr
						|
						(RelopExprZeroOne_epsilon)
						;

Relop ::=   (Relop_first) EQUAL
			|
			(Relop_second) DIFFERENT
			|
			(Relop_third) GREATERTHEN
			|
			(Relop_fourth) GREATERTHENOREQUAL
			|
			(Relop_fifth) LESSTHEN
			|
			(Relop_sixth) LESSTHENOREQUAL
			;				
			
// ============================================================
// EXPRESSIONS 
// ============================================================

Expr ::=    (Expr_ternary) Condition QUESTIONMARK Expr COLON Expr
            |
            (Expr_normal) AddExpr
            ;

AddExpr ::= (AddExpr_addop) Term AddopTermList;


AddopTermList ::=   (AddopTermList_rek) AddopTermList Addop Term
                    |
                    (AddopTermList_epsilon) /* epsilon */
                    ;

Addop ::=   (Addop_plus) PLUS
            |
            (Addop_minus) MINUS
            ;

// ============================================================
// TERM 
// ============================================================

Term ::= (Term) MulopFactorList;

MulopFactorList ::= (MulopFactorList_rek) MulopFactorList Mulop Factor   				
					|
					(MulopFactorList_factor) Factor
					; 

Mulop ::=   (Mulop_mul) MUL
			|				
			(Mulop_div) DIV
			|
			(Mulop_percent) PERCENT
			;

// ============================================================
// FACTOR 
// ============================================================

Factor ::=  (Factor) Unary FactorSub;

FactorSub ::=   (FactorSub_des) Designator ActParsBracketsZeroOne
				|
				(FactorSub_num) NUMBER
				|
				(FactorSub_chr) CHARACTER
				|
				(FactorSub_bool) BOOL 
				|
				(FactorSub_new) NEW Type SQUAREOPENBRACKET Expr SQUARECLBRACKET
				|
				(FactorSub_expr) OPENBRACKET Expr CLBRACKET
				; 

Unary ::=   (Unary_minus) MINUS
            |
            (Unary_epsilon) /* epsilon */
            ;
            
// ============================================================
// DESIGNATOR
// ============================================================

				
Designator ::=  (Designator_var) IDENT DesignatorMore
             	| 
             	(Designator_arr) DesignatorArrName SQUAREOPENBRACKET Expr SQUARECLBRACKET DesignatorMore
				;
				
DesignatorArrName ::= (DesignatorArrName) IDENT;


DesignatorMore ::=  (DesignatorMore_dotIdent) DOT IDENT DesignatorMore
                 	| 
                 	(DesignatorMore_dotLength) DOT LENGTH
                 	| 
                 	(DesignatorMore_epsilon) /* epsilon */
					;